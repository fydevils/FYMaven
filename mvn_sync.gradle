apply plugin: 'maven'
apply plugin: 'signing'


configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            pom.artifactId = project['ARTIFACT_ID']
            pom.groupId = project['GROUP_ID']

            def USERNAME = project.getProperties().get("USERNAME")
            def PASSWORD = project.getProperties().get("PASSWORD")
            def BUILD_VERSION = project.getProperties().get("BUILD_VERSION")
            if (null != BUILD_VERSION) {
                pom.version = BUILD_VERSION
            }
            repository(url: project['MAVEN_REPO_RELEASE_URL']) {
                authentication(userName: USERNAME, password: PASSWORD)
            }
            snapshotRepository(url: project['MAVEN_REPO_SNAPSHOT_URL']) {
                authentication(userName: USERNAME, password: PASSWORD)
            }
        }
    }
}

// 生成sources.jar
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

// 产生相关配置文件的任务
artifacts {
    archives androidSourcesJar
}

// 进行数字签名
signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}